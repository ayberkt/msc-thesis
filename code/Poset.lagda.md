```
{-# OPTIONS --without-K --cubical --safe #-}

module Poset where

open import Basis
open import Cubical.Foundations.SIP renaming (SNS-â‰¡ to SNS)
open import Powerset
open import Function using (_âˆ˜_; id)
```

## Definition of poset

```
Order : (â„“â‚ : Level) â†’ Type â„“ â†’ Type (â„“ âŠ” suc â„“â‚)
Order â„“â‚ A = A â†’ A â†’ hProp â„“â‚

isReflexive : {A : Type â„“â‚€} â†’ Order â„“â‚ A â†’ hProp (â„“â‚€ âŠ” â„“â‚)
isReflexive {A = X} _âŠ‘_ =
  ((x : X) â†’ [ x âŠ‘ x ]) , isPropÎ  (Î» x â†’ is-true-prop (x âŠ‘ x))

isTransitive : {A : Type â„“â‚€} â†’ Order â„“â‚ A â†’ hProp (â„“â‚€ âŠ” â„“â‚)
isTransitive {â„“â‚€ = â„“â‚€} {â„“â‚ = â„“â‚} {A = X} _âŠ‘_ = âŠ‘-trans , âŠ‘-trans-prop
  where
    âŠ‘-trans : Type (â„“â‚€ âŠ” â„“â‚)
    âŠ‘-trans = ((x y z : X) â†’ [ x âŠ‘ y â‡’ y âŠ‘ z â‡’ x âŠ‘ z ])

    âŠ‘-trans-prop : isProp  âŠ‘-trans
    âŠ‘-trans-prop = isPropÎ 3 Î» x y z â†’ is-true-prop (x âŠ‘ y â‡’ y âŠ‘ z â‡’ x âŠ‘ z)

isAntisym : {A : Type â„“â‚€} â†’ isSet A â†’ Order â„“â‚ A â†’ hProp (â„“â‚€ âŠ” â„“â‚)
isAntisym {A = A} A-set _âŠ‘_ = âŠ‘-antisym , âŠ‘-antisym-prop
  where
    âŠ‘-antisym = (x y : A) â†’ [ x âŠ‘ y ] â†’ [ y âŠ‘ x ] â†’ x â‰¡ y

    âŠ‘-antisym-prop : isProp âŠ‘-antisym
    âŠ‘-antisym-prop = isPropÎ 2 Î» x y â†’ isPropÎ 2 Î» _ _ â†’ A-set x y

PosetAx : (A : Type â„“â‚€) â†’ Order â„“â‚ A â†’ hProp (â„“â‚€ âŠ” â„“â‚)
PosetAx {â„“â‚€ = â„“â‚€} {â„“â‚ = â„“â‚} A _âŠ‘_ = isAPartialSet , isAPartialSet-prop
  where
    isAPartialSet =
      Î£[ A-set âˆˆ isSet A ] [ isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_ ]

    isAPartialSet-prop =
      isPropÎ£ isPropIsSet Î» A-set â†’
        is-true-prop (isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_)
```

A poset structure with level `â„“â‚`.

```
PosetStr : (â„“â‚ : Level) â†’ Type â„“ â†’ Type (â„“ âŠ” suc â„“â‚)
PosetStr â„“â‚ = add-to-structure (Order â„“â‚) Î» A _âŠ‘_ â†’ [ PosetAx A _âŠ‘_ ]

PosetStr-set : (â„“â‚ : Level) (A : Type â„“â‚€) â†’ isSet (PosetStr â„“â‚ A)
PosetStr-set â„“â‚ A =
  isSetÎ£ (isSetÎ  Î» _ â†’ isSetÎ  Î» _ â†’ isSetHProp) Î» _âŠ‘_ â†’
  isSetÎ£ (isPropâ†’isSet isPropIsSet) Î» A-set â†’
    isPropâ†’isSet (is-true-prop (isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_))
```

A poset with carrier level `â„“â‚€` and relation level `â„“â‚`.

```
Poset : (â„“â‚€ â„“â‚ : Level) â†’ Type (suc â„“â‚€ âŠ” suc â„“â‚)
Poset â„“â‚€ â„“â‚ = Î£ (Type â„“â‚€) (PosetStr â„“â‚)
```

## Projections

Given a poset `P`, `âˆ£ P âˆ£â‚š` denotes its carrier set and `strâ‚š P` its order structure.

```
âˆ£_âˆ£â‚š : Poset â„“â‚€ â„“â‚ â†’ Type â„“â‚€
âˆ£ X , _ âˆ£â‚š = X

strâ‚š : (P : Poset â„“â‚€ â„“â‚) â†’ PosetStr â„“â‚ âˆ£ P âˆ£â‚š
strâ‚š (_ , s) = s
```

We refer to to the order of `P` as `_âŠ‘[ P ]_`.

```
rel : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ hProp â„“â‚
rel (_ , _âŠ‘_ , _) = _âŠ‘_

infix 9 rel

syntax rel P x y = x âŠ‘[ P ] y

```

Similarly, we define projections for the poset properties.

```
âŠ‘[_]-refl : (P : Poset â„“â‚€ â„“â‚) â†’ (x : âˆ£ P âˆ£â‚š) â†’ [ x âŠ‘[ P ] x ]
âŠ‘[_]-refl (_ , _ , _ , âŠ‘-refl , _) = âŠ‘-refl

âŠ‘[_]-trans : (P : Poset â„“â‚€ â„“â‚) (x y z : âˆ£ P âˆ£â‚š)
           â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] z ] â†’ [ x âŠ‘[ P ] z ]
âŠ‘[_]-trans (_ , _ , _ , _ , âŠ‘-trans , _) = âŠ‘-trans

âŠ‘[_]-antisym : (P : Poset â„“â‚€ â„“â‚) (x y : âˆ£ P âˆ£â‚š)
             â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] x ] â†’ x â‰¡ y
âŠ‘[_]-antisym (_ , _ , _ , _ , _ , âŠ‘-antisym) = âŠ‘-antisym

carrier-is-set : (P : Poset â„“â‚€ â„“â‚) â†’ isSet âˆ£ P âˆ£â‚š
carrier-is-set (_ , _ , is-set , _) = is-set
```

## Partial order reasoning

```
module PosetReasoning (P : Poset â„“â‚€ â„“â‚) where

  _âŠ‘âŸ¨_âŸ©_ : (x : âˆ£ P âˆ£â‚š) {y z : âˆ£ P âˆ£â‚š}
         â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] z ] â†’ [ x âŠ‘[ P ] z ]
  _ âŠ‘âŸ¨ p âŸ© q = âŠ‘[ P ]-trans _ _ _ p q

  _â–  : (x : âˆ£ P âˆ£â‚š) â†’ [ x âŠ‘[ P ] x ]
  _â–  = âŠ‘[ P ]-refl

  infixr 0 _âŠ‘âŸ¨_âŸ©_
  infix  1 _â– 
```

```
â‰¡â‡’âŠ‘ : (P : Poset â„“â‚€ â„“â‚) â†’ {x y : âˆ£ P âˆ£â‚š} â†’ x â‰¡ y â†’ [ x âŠ‘[ P ] y ]
â‰¡â‡’âŠ‘ P {x = x} p = subst (Î» z â†’ [ x âŠ‘[ P ] z ]) p (âŠ‘[ P ]-refl x)
```

## Monotonic functions

```
isMonotonic : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚‚ â„“â‚ƒ)
            â†’ (âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š) â†’ Type (â„“â‚€ âŠ” â„“â‚ âŠ” â„“â‚ƒ)
isMonotonic P Q f = (x y : âˆ£ P âˆ£â‚š) â†’ [ x âŠ‘[ P ] y ] â†’ [ (f x) âŠ‘[ Q ] (f y) ]

isMonotonic-prop : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²) (f : âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š)
                 â†’ isProp (isMonotonic P Q f)
isMonotonic-prop P Q f =
  isPropÎ  (Î» x â†’ isPropÎ  Î» y â†’ isPropÎ  Î» _ â†’ is-true-prop (f x âŠ‘[ Q ] f y))

```

We collect monotonic functions in the following type.

```
_â”€mâ†’_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€â€² â„“â‚â€² â†’ Type (â„“â‚€ âŠ” â„“â‚ âŠ” â„“â‚€â€² âŠ” â„“â‚â€²)
_â”€mâ†’_ P Q = Î£ (âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š) (isMonotonic P Q)
```

```
poset-isoâ€² : (P Q : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š â†’ Type (â„“â‚€ âŠ” â„“â‚)
poset-isoâ€² P Q e = isMonotonic P Q f Ã— isMonotonic Q P g
  where
    f = Ï€â‚€ (equivâ†’HAEquiv e)
    g = isHAEquiv.g (Ï€â‚ (equivâ†’HAEquiv e))

poset-isoâ€²â€² : (P Q : Poset â„“â‚€ â„“â‚) â†’ (P â”€mâ†’ Q) â†’ Type (â„“â‚€ âŠ” â„“â‚)
poset-isoâ€²â€² P Q (f , _) = Î£[ (g , _) âˆˆ (Q â”€mâ†’ P) ] section f g Ã— retract f g
```

Projection for the underlying function of a monotonic map.

```
_$â‚˜_ = Ï€â‚€
```

The identity monotonic map and composition of monotonic maps.

```
ğŸm : (P : Poset â„“â‚€ â„“â‚) â†’ P â”€mâ†’ P
ğŸm P = id , (Î» x y xâŠ‘y â†’ xâŠ‘y)

_âˆ˜m_ : {P : Poset â„“â‚€ â„“â‚} {Q : Poset â„“â‚€â€² â„“â‚â€²} {R : Poset â„“â‚€â€²â€² â„“â‚â€²â€²}
     â†’ (Q â”€mâ†’ R) â†’ (P â”€mâ†’ Q) â†’ (P â”€mâ†’ R)
(g , pg) âˆ˜m (f , pf) = g âˆ˜ f , Î» x y p â†’ pg (f x) (f y) (pf x y p)
```

## Downward-closure

We denote by `â†“[ P ] x` the type of everything in `P` that is below `x`.

```
â†“[_]_ : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ Type (â„“â‚€ âŠ” â„“â‚)
â†“[ P ] a = Î£[ b âˆˆ âˆ£ P âˆ£â‚š ] [ b âŠ‘[ P ] a ]
```

```
isDownwardsClosed : (P : Poset â„“â‚€ â„“â‚) â†’ ğ’« âˆ£ P âˆ£â‚š â†’ hProp (â„“â‚€ âŠ” â„“â‚)
isDownwardsClosed P U =
  ((x y : âˆ£ P âˆ£â‚š) â†’ [ x âˆˆ U ] â†’ [ y âŠ‘[ P ] x ] â†’ [ y âˆˆ U ]) , prop
  where
    prop : isProp ((x y : âˆ£ P âˆ£â‚š) â†’ [ U x ] â†’ [ y âŠ‘[ P ] x ] â†’ [ U y ])
    prop = isPropÎ  Î» _ â†’ isPropÎ  Î» x â†’ isPropÎ  Î» _ â†’ isPropÎ  Î» _ â†’ is-true-prop (x âˆˆ U)

DCSubset : (P : Poset â„“â‚€ â„“â‚) â†’ Type (suc â„“â‚€ âŠ” â„“â‚)
DCSubset P = Î£[ U âˆˆ ğ’« âˆ£ P âˆ£â‚š ] [ isDownwardsClosed P U ]

DCSubset-set : (P : Poset â„“â‚€ â„“â‚) â†’ isSet (DCSubset P)
DCSubset-set P =
  isSetÎ£ (ğ’«-set âˆ£ P âˆ£â‚š) Î» U â†’ isPropâ†’isSet (is-true-prop (isDownwardsClosed P U))
```

## Product of two posets

```
_Ã—â‚š_ : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²) â†’ Poset (â„“â‚€ âŠ” â„“â‚€â€²) (â„“â‚ âŠ” â„“â‚â€²)
P Ã—â‚š Q = (âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) , _âŠ‘_ , carrier-set , (âŠ‘-refl , âŠ‘-trans , âŠ‘-antisym)
  where
    _âŠ‘_ : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š â†’ âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š â†’ hProp _
    _âŠ‘_ (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) = xâ‚€ âŠ‘[ P ] xâ‚ âŠ“ yâ‚€ âŠ‘[ Q ] yâ‚

    carrier-set : isSet (âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š)
    carrier-set = isSetÎ£ (carrier-is-set P) Î» _ â†’ (carrier-is-set Q)

    âŠ‘-refl : (p : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ p ]
    âŠ‘-refl (x , y) = (âŠ‘[ P ]-refl x) , (âŠ‘[ Q ]-refl y)

    âŠ‘-trans : (p q r : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ q ] â†’ [ q âŠ‘ r ] â†’ [ p âŠ‘ r ]
    âŠ‘-trans (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (xâ‚‚ , yâ‚‚) (xâ‚€âŠ‘xâ‚ , yâ‚€âŠ‘yâ‚) (xâ‚âŠ‘xâ‚‚ , yâ‚âŠ‘yâ‚‚) =
      âŠ‘[ P ]-trans _ _ _ xâ‚€âŠ‘xâ‚ xâ‚âŠ‘xâ‚‚ , âŠ‘[ Q ]-trans _ _ _ yâ‚€âŠ‘yâ‚ yâ‚âŠ‘yâ‚‚

    âŠ‘-antisym : (p q : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ q ] â†’ [ q âŠ‘ p ] â†’ p â‰¡ q
    âŠ‘-antisym (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (xâ‚€âŠ‘xâ‚ , yâ‚€âŠ‘yâ‚) (xâ‚âŠ‘xâ‚€ , yâ‚âŠ‘yâ‚€) =
      sigmaPathâ†’pathSigma (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (âŠ‘[ P ]-antisym _ _ xâ‚€âŠ‘xâ‚ xâ‚âŠ‘xâ‚€ , sym NTS)
      where
        NTS : yâ‚ â‰¡ transport refl yâ‚€
        NTS = subst (_â‰¡_ yâ‚) (sym (transportRefl yâ‚€)) (âŠ‘[ Q ]-antisym _ _ yâ‚âŠ‘yâ‚€ yâ‚€âŠ‘yâ‚)
```

## Equality of isomorphic posets

```
order-iso : (M N : Î£ (Type â„“â‚€) (Order â„“â‚)) â†’ Ï€â‚€ M â‰ƒ Ï€â‚€ N â†’ Type (â„“â‚€ âŠ” â„“â‚)
order-iso (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_) eqv =
  (x y : A) â†’ [ x âŠ‘â‚€ y â‡” f x âŠ‘â‚ f y ]
  where
    f = equivFun eqv

isSet-Order : (â„“â‚ : Level) (A : Type â„“â‚€) â†’ isSet (Order â„“â‚ A)
isSet-Order â„“â‚ A = isSetÎ  Î» _ â†’ isSetÎ  Î» _ â†’ isSetHProp

Order-is-SNS : SNS {â„“} (Order â„“â‚) order-iso
Order-is-SNS {â„“â‚ = â„“â‚} {X = X}  _âŠ‘â‚€_ _âŠ‘â‚_ = f , f-equiv
  where
    f : order-iso (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (idEquiv X) â†’ _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_
    f i = funExt Î» x â†’ funExt Î» y â†’ â‡”toPath (Ï€â‚€ (i x y)) (Ï€â‚ (i x y))

    â‡”-prop : isProp ((x y : X) â†’ [ x âŠ‘â‚€ y â‡” x âŠ‘â‚ y ])
    â‡”-prop = isPropÎ  Î» x â†’ isPropÎ  Î» y â†’ is-true-prop (x âŠ‘â‚€ y â‡” x âŠ‘â‚ y)

    f-equiv : isEquiv f
    f-equiv = record { equiv-proof = Î» eq â†’ (g eq , sec eq) , h eq }
      where
        g : (eq : _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_)
          â†’ (x y : X)
          â†’ ([ x âŠ‘â‚€ y ] â†’ [ x âŠ‘â‚ y ]) Ã— ([ x âŠ‘â‚ y ] â†’ [ x âŠ‘â‚€ y ])
        g eq x y = subst (Î» { _âŠ‘â‹†_ â†’ [ x âŠ‘â‹† y ] }) eq
                 , subst (Î» { _âŠ‘â‹†_ â†’ [ x âŠ‘â‹† y ] }) (sym eq)

        sec : section f g
        sec p = isSet-Order _ X _âŠ‘â‚€_ _âŠ‘â‚_ (f (g p)) p

        h : (p : _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_) â†’ (fib : fiber f p) â†’ (g p , sec p) â‰¡ fib
        h p (i , _) = Î£Propâ‰¡
                        (Î» iâ€² â†’ isOfHLevelSuc 2 (isSet-Order â„“â‚ X) _âŠ‘â‚€_ _âŠ‘â‚_ (f iâ€²) p)
                        (â‡”-prop (g p) i)


RP-iso-prop : (P Q : Î£ (Type â„“â‚€) (Order â„“â‚))
            â†’ (i : Ï€â‚€ P â‰ƒ Ï€â‚€ Q) â†’ isProp (order-iso P Q i)
RP-iso-prop (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_) i =
  isPropÎ  Î» x â†’ isPropÎ  Î» y â†’ is-true-prop (x âŠ‘â‚€ y â‡” f x âŠ‘â‚ f y)
  where
    f = equivFun i

poset-iso : (P Q : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š â†’ Type (â„“â‚€ âŠ” â„“â‚)
poset-iso {â„“â‚ = â„“â‚} = add-to-iso order-iso Î» A _âŠ‘_ â†’ [ PosetAx A _âŠ‘_ ]

poset-isoâ‡”poset-isoâ€² : (P Q : Poset â„“â‚€ â„“â‚) (e : âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š)
                     â†’ (poset-iso P Q e â†’ poset-isoâ€² P Q e)
                     Ã— (poset-isoâ€² P Q e â†’ poset-iso P Q e)
poset-isoâ‡”poset-isoâ€² P Q e = to , from
  where
    f   = Ï€â‚€ (equivâ†’HAEquiv e)
    g   = isHAEquiv.g (Ï€â‚ (equivâ†’HAEquiv e))
    sec : section f g
    sec = isHAEquiv.ret (Ï€â‚ (equivâ†’HAEquiv e))
    ret : retract f g
    ret = isHAEquiv.sec (Ï€â‚ (equivâ†’HAEquiv e))

    to : poset-iso P Q e â†’ poset-isoâ€² P Q e
    to i = f-mono , g-mono
      where

        f-mono : isMonotonic P Q f
        f-mono x y xâŠ‘y = Ï€â‚€ (i x y) xâŠ‘y
        g-mono : isMonotonic Q P g
        g-mono x y xâŠ‘y =  Ï€â‚ (i (g x) (g y)) NTS
          where
            NTS : [ f (g x) âŠ‘[ Q ] (f (g y)) ]
            NTS = subst (Î» - â†’ [ rel Q (- x) (- y) ]) (sym (funExt sec)) xâŠ‘y

    from : poset-isoâ€² P Q e â†’ poset-iso P Q e
    from i x y = Ï† , Ïˆ
      where
        Ï† : [ x âŠ‘[ P ] y ] â†’ [ f x âŠ‘[ Q ] f y ]
        Ï† xâŠ‘y = Ï€â‚€ i x y xâŠ‘y
        Ïˆ : [ f x âŠ‘[ Q ] f y ] â†’ [ x âŠ‘[ P ] y ]
        Ïˆ fxâŠ‘fy = subst (Î» - â†’ [ - x âŠ‘[ P ] - y ]) (funExt ret) (Ï€â‚ i (f x) (f y) fxâŠ‘fy)

poset-axioms-props : (A : Type â„“â‚€) (str : Order â„“â‚ A)
                   â†’ isProp [ PosetAx A str ]
poset-axioms-props {â„“â‚ = â„“â‚} A str = is-true-prop (PosetAx A str)


poset-is-SNS : SNS {â„“} (PosetStr â„“â‚) poset-iso
poset-is-SNS {â„“â‚ = â„“â‚} =
  SNS-PathPâ†’SNS-â‰¡
    (PosetStr â„“â‚)
    poset-iso
    (add-axioms-SNS _ poset-axioms-props (SNS-â‰¡â†’SNS-PathP order-iso Order-is-SNS))

poset-is-SNS-PathP : SNS-PathP {â„“} (PosetStr â„“â‚) poset-iso
poset-is-SNS-PathP = SNS-â‰¡â†’SNS-PathP poset-iso poset-is-SNS

poset-SIP : (A : Type â„“â‚€) (B : Type â„“â‚€) (eqv : A â‰ƒ B)
            (P : PosetStr â„“â‚ A) (Q : PosetStr â„“â‚ B)
          â†’ poset-iso (A , P) (B , Q) eqv
          â†’ (A , P) â‰¡ (B , Q)
poset-SIP {â„“â‚ = â„“â‚} A B eqv P Q i = foo (eqv , i)
  where
    foo : (A , P) â‰ƒ[ poset-iso ] (B , Q) â†’ (A , P) â‰¡ (B , Q)
    foo = equivFun (SIP poset-is-SNS-PathP (A , P) (B , Q))

_â‰ƒâ‚š_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€ â„“â‚ â†’ Type (â„“â‚€ âŠ” â„“â‚)
_â‰ƒâ‚š_ P Q = Î£[ i âˆˆ (âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š) ] poset-iso P Q i

_â‰ƒâ‚šâ€²_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€ â„“â‚ â†’ Type (â„“â‚€ âŠ” â„“â‚)
P â‰ƒâ‚šâ€² Q = Î£[ eqv âˆˆ (âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š) ] poset-isoâ€² P Q eqv

_â‰ƒâ‹†_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€ â„“â‚ â†’ Type (â„“â‚€ âŠ” â„“â‚)
P â‰ƒâ‹† Q = Î£[ f âˆˆ (P â”€mâ†’ Q) ] poset-isoâ€²â€² P Q f

pos-iso-to-eq : (P Q : Poset â„“â‚€ â„“â‚) â†’ P â‰ƒâ‚š Q â†’ P â‰¡ Q
pos-iso-to-eq (A , A-pos) (B , B-pos) (eqv , i) = poset-SIP A B eqv A-pos B-pos i

pos-iso-to-eqâ€² : (P Q : Poset â„“â‚€ â„“â‚) â†’ P â‰ƒâ‚šâ€² Q â†’ P â‰¡ Q
pos-iso-to-eqâ€² P Q (eqv , i-homo) =
  pos-iso-to-eq P Q (eqv , Ï€â‚ (poset-isoâ‡”poset-isoâ€² P Q eqv) i-homo)

â‰ƒâ‹†â†’â‰ƒâ‚šâ€² : (P Q : Poset â„“â‚€ â„“â‚) â†’ P â‰ƒâ‹† Q â†’ P â‰ƒâ‚šâ€² Q
â‰ƒâ‹†â†’â‰ƒâ‚šâ€² P Q ((f , f-mono) , (g , g-mono) , sec , ret) =
  isoToEquiv (iso f g sec ret) , f-mono , g-mono

-- --}
-- --}
-- --}
```
